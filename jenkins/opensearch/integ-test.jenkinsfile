lib = library(identifier: "jenkins@20211118", retriever: legacySCM(scm))

pipeline {
    agent none
    environment {
        BUILD_MANIFEST = "build-manifest.yml"
    }
    tools {
        jdk "JDK14"
        maven "maven-3.8.2"
    }
    parameters {
        string(
            name: 'TEST_MANIFEST',
            description: 'Test manifest under the manifests folder, e.g. 2.0.0/opensearch-2.0.0-test.yml.',
            trim: true
        )
        string(
            name: 'BUILD_MANIFEST_URL',
            description: 'The build manifest URL, e.g. https://ci.opensearch.org/ci/dbc/distribution-build-opensearch/1.2.2/98/linux/x64/builds/opensearch/manifest.yml.',
            trim: true
        )
        string(
            name: 'AGENT_LABEL',
            description: 'The agent label where the tests should be executed, e.g. Jenkins-Agent-al2-x64-c54xlarge-Docker-Host.',
            trim: true
        )
    }
    stages {
        stage('verify-parameters') {
            steps {
                script {
                    if (AGENT_LABEL == '') {
                        currentBuild.result = 'ABORTED'
                        error("Integration Tests failed to start. Missing parameter: AGENT_LABEL.")
                    }
                }
            }
        }
        stage('integ-test') {
            agent {
                node {
                    label AGENT_LABEL
                }
            }
            steps {
                script {
                    def buildManifestObj = downloadBuildManifest(
                        url: BUILD_MANIFEST_URL,
                        path: BUILD_MANIFEST
                    )
                    String buildId = buildManifestObj.getArtifactBuildId()
                    env.BUILD_ID = buildId
                    echo "BUILD_MANIFEST: ${BUILD_MANIFEST}"
                    echo "BUILD_ID: ${BUILD_ID}"

                    runIntegTestScript(
                        buildManifest: BUILD_MANIFEST,
                        testManifest: "manifests/${TEST_MANIFEST}",
                        buildId: BUILD_ID
                    )
                }
            }
            post {
                always {
                    script {
                        uploadTestResults(
                            buildManifestFileName: BUILD_MANIFEST,
                            jobName: JOB_NAME,
                            buildNumber: BUILD_ID
                        )
                    }
                    postCleanup()
                }
            }
        }
    }

    post {
        success {
            node(AGENT_LABEL) {
                script {
                    def stashed = lib.jenkins.Messages.new(this).get(['integ-test'])
                    publishNotification(
                        icon: ':white_check_mark:',
                        message: 'Integration Tests Successful',
                        extra: stashed,
                        credentialsId: 'INTEG_TEST_WEBHOOK',
                    )

                    postCleanup()
                }
            }
        }
        failure {
            node(AGENT_LABEL) {
                script  {
                    def stashed = lib.jenkins.Messages.new(this).get(['integ-test'])
                    publishNotification(
                        icon: ':warning:',
                        message: 'Failed Integration Tests',
                        extra: stashed,
                        credentialsId: 'INTEG_TEST_WEBHOOK',
                    )

                    postCleanup()
                }
            }
        }
    }
}
